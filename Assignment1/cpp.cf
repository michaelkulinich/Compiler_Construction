PDefs.   Program ::= [Def] ;

-- Rule for Namespace definitions. starts with 'using'
-- and is followed by a qualified constant
-- Also declarations and initilizations
DUsing. Def ::= "using" Names ";" ;
DTypeDefs.  Def ::= "typedef" Type Id ";" ;
DDecls.     Def ::= Type [Id] ";" ;
DInit.      Def ::= Type Id "=" Exp ";" ;


DFun.       Def    ::= Type Id "(" [Arg] ")" "{" [Stm] "}" ;
DFunDec.    Def    ::= Type Id "(" [Arg] ")" ";" ;  --Function declarations
DInlineFun.  Def    ::= "inline" Def ;
DStruct.    Def    ::= "struct" Id "{" [SDef] "}" ";" ; -- Struct rule
terminator Def "" ;

-- declarations inside of structs
StructDecls. SDef ::= Type [Id] ";" ;
terminator SDef "" ;

ADecl.      Arg    ::= Type Id ;
separator Arg "," ;

-- Rule for function declarations, arguments arent given Id
ADecltwo.      Arg    ::= Type;

SExp.        Stm ::= Exp ";" ;

-- Rule for type definitions
STypeDef.    Stm ::= "typedef" Type [Id] ";" ;
SDecls.      Stm ::= Type [Id] ";" ;
SInit.       Stm ::= Type Id "=" Exp ";" ;
SReturn.     Stm ::= "return" Exp ";" ;
SReturnVoid. Stm ::= "return" ";" ;
SForLoop.    Stm ::= "for" "(" Stm Exp ";" Exp ")" Stm ;
SDoWhile.    Stm ::= "do" Stm "while" "(" Exp ")" ";" ;
SWhile.      Stm ::= "while" "(" Exp ")" Stm ;
SBlock.      Stm ::= "{" [Stm] "}" ;

-- A singlular if statement
SIf.         Stm ::= "if" "(" Exp ")" Stm ;
SIfElse.     Stm ::= "if" "(" Exp ")" Stm "else" Stm ;

-- Struct decl in function
SStruct.    Stm    ::= "struct" Id "{" [Stm] "}" ";" ;
terminator Stm "" ;

ETrue.   Exp15  ::= "true" ;
EFalse.  Exp15  ::= "false" ;
EInt.    Exp15  ::= Integer ;
EDouble. Exp15  ::= Double ;
EChar.   Exp15  ::= Char ; --rule for char

-- Made this into a list of Strings for when a sentence
-- is split into two lines of code
EString. Exp15  ::= [String] ;
separator nonempty String "" ;

-- rules for qualified constants and indexing
EIndexing. Exp15 ::= Exp15 "[" Exp "]" ;
EQConsts.           Exp15 ::=  Names ;
EQConst.            Names ::= [Name] ;
EQConstTemp.        Name  ::= Name "<" [Type] ">" ;
EQConstNameId.      Name  ::= Id ;

-- Qualified constants are a nonempty list seperated by "::"
separator nonempty Name "::" ;
separator nonempty Type "," ;

-- This might be an issue, it should be Id instead of Name
EApp.    Exp15  ::= Name "(" [Exp] ")" ;


-- used for function calls. eg: myVector.size()
EProj.   Exp14 ::= Exp14 "." Exp15 ;
EArrow.  Exp14 ::= Exp14 "->" Exp15 ;

EDeRef.  Exp14  ::= "*" Exp15 ;
EPIncr.  Exp14  ::= Exp15 "++" ;
EPDecr.  Exp14  ::= Exp15 "--" ;

EIncr.   Exp13  ::= "++" Exp14 ;
EDecr.   Exp13  ::= "--" Exp14 ;

EMod.    Exp12  ::= Exp12 "%"  Exp13 ;
ETimes.  Exp12  ::= Exp12 "*"  Exp13 ;
EDiv.    Exp12  ::= Exp12 "/"  Exp13 ;
EPlus.   Exp11  ::= Exp11 "+"  Exp12 ;
EMinus.  Exp11  ::= Exp11 "-"  Exp12 ;

-- rules for left and right shift
ELShift.  Exp10  ::= Exp10 "<<" Exp11 ;
ERShift.  Exp10  ::= Exp10 ">>" Exp11 ;

ENot.    Exp10 ::= "!" Exp10 ;
ELt.     Exp9   ::= Exp9  "<"  Exp10 ;
EGt.     Exp9   ::= Exp9  ">"  Exp10 ;
ELtEq.   Exp9   ::= Exp9  "<=" Exp10 ;
EGtEq.   Exp9   ::= Exp9  ">=" Exp10 ;
EEq.     Exp8   ::= Exp8  "==" Exp9 ;
ENEq.    Exp8   ::= Exp8  "!=" Exp9 ;
EAnd.    Exp4   ::= Exp4  "&&" Exp5 ;
EOr.     Exp3   ::= Exp3  "||" Exp4 ;
EAss.    Exp2   ::= Exp3 "=" Exp2 ;
EAssP.   Exp2   ::= Exp3 "+=" Exp2 ; -- plus equals
EAssM.   Exp2   ::= Exp3 "-=" Exp2 ; -- minus equals


-- rule for conditional
ECond.   Exp2   ::= Exp2 "?" Exp3 ":" Exp3 ;

-- rule for exceptions
EExcep.  Exp1   ::= "throw" Exp2 ;
internal ETyped. Exp15 ::= "(" Exp ":" Type ")" ;

coercions Exp 15 ;

separator Exp "," ;

-- added Names to type becuase types are either qualified constants
-- or built in types
rules Type   ::= "bool" | "int" | "double" | "void" | "char" | Names | Type "&" | "const" Type ;

token Id (letter (letter | digit | '_')*) ;


separator nonempty Id "," ;



comment "#" ;
comment "//" ;
comment "/*" "*/" ;
